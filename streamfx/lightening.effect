#include "../includes/streamfx.effect"
#include "../includes/constants.effect"
#include "../includes/transition-fns.effect"


uniform float4 Time<
	bool automatic = true;
>;

/*
 *  Electricity effect ported from sqrt_1 "Electro": https://www.shadertoy.com/view/4scGWj
 *  Simplex noise ported from Nikita Miropolskiy: https://www.shadertoy.com/view/XsX3zB
 */

uniform float RevealDistance<
  string name = "Reveal Distance";
  string field_type = "slider";
  float minimum = 0.0;
  float maximum = 100.1;
  float step = 0.1;
  float scale = 0.01;
> = 0.0;

uniform float TransitionAngle<
  string name = "Reveal Angle";
  string field_type = "slider";
  float minimum = -180.0;
  float maximum = 180.0;
  float step = 0.1;
> = 0.0;

uniform bool TimeEvolution<
  string name = "Time-based evolution";
> = true;

uniform float3 Color<
  string name = "Color";
  string field_type = "slider";
  float3 minimum = {0.0, 0.0, 0.0};
  float3 maximum = {255.0, 255.0, 255.0};
  float3 step = {0.1, 0.1, 0.1};
  float3 scale = {0.0039216, 0.0039216, 0.0039216};
> = {255., 255., 255.};

uniform float Speed<
  string name = "Speed";
  string field_type = "slider";
  float minimum = 1.0;
  float maximum = 100.0;
  float step = 0.1;
> = 10.0;

uniform float NoiseScale<
  string name = "Noise Scale";
  string field_type = "slider";
  float minimum = 1.0;
  float maximum = 1500.0;
  float step = 1.0;
> = 20.0;

uniform float Intensity<
  string name = "Bolt Intensity";
  string field_type = "slider";
  float minimum = 0.01;
  float maximum = 0.5;
  float step = 0.01;
> = 0.2;

sampler_state maskSampler {
	Filter		= Linear;
	AddressU	= Clamp;
	AddressV	= Clamp;
};

/* discontinuous pseudorandom uniformly distributed in [-0.5, +0.5]^3 */
float3 random3(float3 c) {
	float j = 4096.0*sin(dot(c,float3(17.0, 59.4, 15.0)));
	float3 r;
	r.z = frac(512.0*j);
	j *= .125;
	r.x = frac(512.0*j);
	j *= .125;
	r.y = frac(512.0*j);
	return r-0.5;
}

/* skew constants for 3d simplex functions */
const float F3 =  0.3333333;
const float G3 =  0.1666667;

/* 3d simplex noise */
float simplex3d(float3 p) {
	 /* 1. find current tetrahedron T and it's four vertices */
	 /* s, s+i1, s+i2, s+1.0 - absolute skewed (integer) coordinates of T vertices */
	 /* x, x1, x2, x3 - unskewed coordinates of p relative to each of T vertices*/
	 
	 /* calculate s and x */
	 float3 s = floor(p + dot(p, float3(F3, F3, F3)));
	 float3 x = p - s + dot(s, float3(G3, G3, G3));
	 
	 /* calculate i1 and i2 */
	 float3 e = step(float3(0.0, 0.0, 0.0), x - x.yzx);
	 float3 i1 = e*(1.0 - e.zxy);
	 float3 i2 = 1.0 - e.zxy*(1.0 - e);
	 	
	 /* x1, x2, x3 */
	 float3 x1 = x - i1 + G3;
	 float3 x2 = x - i2 + 2.0*G3;
	 float3 x3 = x - 1.0 + 3.0*G3;
	 
	 /* 2. find four surflets and store them in d */
	 float4 w, d;
	 
	 /* calculate surflet weights */
	 w.x = dot(x, x);
	 w.y = dot(x1, x1);
	 w.z = dot(x2, x2);
	 w.w = dot(x3, x3);
	 
	 /* w fades from 0.6 at the center of the surflet to 0.0 at the margin */
	 w = max(0.6 - w, 0.0);
	 
	 /* calculate surflet components */
	 d.x = dot(random3(s), x);
	 d.y = dot(random3(s + i1), x1);
	 d.z = dot(random3(s + i2), x2);
	 d.w = dot(random3(s + 1.0), x3);
	 
	 /* multiply d by w^4 */
	 w *= w;
	 w *= w;
	 d *= w;
	 
	//  /* 3. return the sum of the four surflets */
	 return dot(d, float4(52.0, 52.0, 52.0, 52.0));
}

float noise(float3 m) {
    return   0.5333333*simplex3d(m)
			+0.2666667*simplex3d(2.0*m)
			+0.1333333*simplex3d(4.0*m)
			+0.0666667*simplex3d(8.0*m);
}

float electricity(float2 v_in_uv, float view_x, float2 coord, float dist, float gp) {
  float t = Time.x;
  float2 uv = v_in_uv;
  uv = uv * 2. - 1.;

  float2 p = coord/view_x;
  float z = TimeEvolution ? t/Speed : 20.0 * dist/Speed;
  float3 p3 = float3(p.x*1.0, p.y, z);

  float intensity = noise(NoiseScale*p3 + 100.0);

  float y = abs(-0.15*intensity + 2.0 * (v_in_uv[1]));
  float g = pow(y, gp);
  return g;
}

float2 TranslateRotate(float2 global, float2 hk, float a) {
  float2 xy_prime = float2(
    cos(a) * global[0] - sin(a) * global[1] + hk[0] * cos(a) - hk[1] * sin(a),
    sin(a) * global[0] + cos(a) * global[1] + hk[0] * sin(a) + hk[1] * cos(a)
  );
  return xy_prime;
}


float4 PSDefault(VertData v_in) : TARGET {
  float epsilon = EPS;
  float2 frame_coord = v_in.uv * ViewSize.xy;
  float dist = 1.16 * RevealDistance - 0.08;

  float radAngle = TransitionAngle * DEG2RAD;
  float planeAngle = (90 - TransitionAngle) * DEG2RAD;

  bool horizontalAngle = abs(sin(radAngle)) < epsilon;
  bool verticalAngle = abs(cos(radAngle)) < epsilon;

  float transition_size = 0.0;

  // Transform global pixel coordinate to local coordinate
  // system where transition is at y=0 and is horizontal.

  float2 endIntersect = edgeIntersection(radAngle);
  float2 startIntersect = {
    abs(endIntersect.x - ViewSize.x),
    abs(endIntersect.y - ViewSize.y)
  };

  float2 delta = {0., 0.};
  if(!horizontalAngle && !verticalAngle) {
    delta = StartStopOffset(endIntersect, radAngle, transition_size);
  } else {
    delta = float2(
      transition_size * cos(radAngle),
      -transition_size * sin(radAngle)
    );
  }

  float2 endPoint = float2(endIntersect.x + delta.x, endIntersect.y + delta.y);
  float2 startPoint = float2(
    startIntersect.x - (delta.x),
    startIntersect.y - (delta.y)
  );
  float2 curPoint = startPoint + dist * (endPoint-startPoint);
  float2 xy_prime = TranslateRotate(frame_coord, -curPoint, -planeAngle);
  float2 uv_prime = xy_prime / ViewSize.xy;

  // Get electricity intensity
  float g = electricity(uv_prime, ViewSize.xy[0], xy_prime, dist, Intensity);

  bool show = uv_prime[1] < -0.05;
  // Detect bottom (masked) edge of transition.
  if(uv_prime[1]<0.05 && uv_prime[1]>=0.00) {
    float2 tuv = uv_prime;
    tuv.y += 0.005;
    while(tuv[1] < 0.5) {
      float2 tcoord = tuv * ViewSize.xy;
      float tg = electricity(tuv, ViewSize.xy[0], tcoord, dist, Intensity);
      if(tg < 0.55) {
        show = true;
        break;
      }
      tuv.y += 0.005;
    }
  }

  // Detect top (unmasked) edge of transition.
  if(uv_prime[1] > -0.05 && uv_prime[1]<=0.00) {
    show=true;
    float2 tuv = uv_prime;
    tuv.y -= 0.005;
    while(tuv.y > -0.05) {
      float2 tcoord = tuv * ViewSize.xy;
      float tg = electricity(tuv, ViewSize.xy[0], tcoord, dist, Intensity);
      if(tg < 0.55) {
        show=false;
        break;
      }
      tuv.y -= 0.005;
    }
  }
  float3 col = float3(2.0, 2.0, 2.0);
  col = col * -g + col;
  col = pow(col, 5);
  float a = pow((col.x + col.y + col.z)/3.0, 1.0);
  col *= Color;
  if(show) {
    float4 bgPixel = InputA.Sample(maskSampler, v_in.uv);
    return float4(col.rgb + bgPixel.rgb * (1.0 - a), 1.0);
  }
  return float4(col, a);
}

technique Draw
{
  pass
  {
    vertex_shader = VSDefault(v_in);
    pixel_shader  = PSDefault(v_in); 
  }
}
